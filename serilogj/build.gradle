apply plugin: 'com.android.library'
apply plugin: 'maven-publish'  // 1. Apply plugin

// 2. Declare library details
ext {
    mGroupId = "com.github.maurosoft1973"
    mArtifactId = "android-serilogj"
    mVersionCode = 1
    mVersionName = "1.0.1"

    mLibraryName = "AndroidSerilogj"
    mLibraryDescription = "Android Serilogj"
}

android {
    compileSdkVersion setup.compileSdk

    defaultConfig {
        buildToolsVersion setup.buildTools
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    //Test
    testImplementation 'junit:junit:' + librarydependencies.junit
    androidTestImplementation 'androidx.test.ext:junit:' + librarydependencies.androidXTestJunit
    androidTestImplementation 'androidx.test.espresso:espresso-core:' + librarydependencies.androidXTestEspressoCore
}

// 3. Declare task for creation of android sources.
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// 4. Make configuration for publishing artifact.
afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

                artifact androidSourcesJar

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }
    }
}

// Assembling should be performed before publishing package
publish.dependsOn assemble
